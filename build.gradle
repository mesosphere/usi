import mesosphere.gradle.aws.S3Upload
import mesosphere.gradle.github.GithubStatus

subprojects {
    version = '1.0'
    apply plugin: "scala"

    repositories {
        jcenter()
    }
    
    dependencies {
        // Use Scala 2.12 in our library project
        implementation 'org.scala-lang:scala-library:2.12.7'

        // Use Scalatest for testing our library
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.scalatest:scalatest_2.12:3.0.5'

        // Need scala-xml at test runtime
        testRuntimeOnly 'org.scala-lang.modules:scala-xml_2.12:1.1.1'
    }

    test {
        reports.html.enabled = false
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task syncContents(type: S3Upload, dependsOn: testReport) {
    bucket = "usi-builds"
    prefix = "mesosphere/usi/karsten/github-report/7" // TODO(karsten): Construct from env vars
    source = file("build/reports/allTests")  // TODO(karsten): Use output from testReport
}

task githubReport(type: GithubStatus) {
    repoSlug = "mesosphere/usi"
    commit = System.getenv().get("TRAVIS_COMMIT")
    context = "mesosphere/tests"
    statusDescription = "Test results"
    targetUrl = "https://examplebucket.s3-website-us-west-2.amazonaws.com/docs/doc1.html"
}
