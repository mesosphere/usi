import mesosphere.gradle.aws.S3Upload
import mesosphere.gradle.github.GithubStatus
import mesosphere.gradle.travis.TravisEnvironment

import java.nio.file.Paths

plugins {
    id 'org.ajoberstar.git-publish' version '2.0.0'
}

subprojects {
    version = '1.0'
    apply plugin: "scala"

    repositories {
        jcenter()
    }
    
    dependencies {
        // Use Scala 2.12 in our library project
        implementation 'org.scala-lang:scala-library:2.12.7'

        // Use Scalatest for testing our library
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.scalatest:scalatest_2.12:3.0.5'

        // Need scala-xml at test runtime
        testRuntimeOnly 'org.scala-lang.modules:scala-xml_2.12:1.1.1'
    }

    test {
        reports.html.enabled = false
    }
    check {}
}

task scaladoc(type: ScalaDoc) {
    description = "Builds all Scala docs into the gh-pages checkout for publishing."
    destinationDir file(Paths.get(rootProject.buildDir.toString(), "gitPublish", "api"))

    source = files(subprojects.collect{it.sourceSets.main.allScala + it.sourceSets.main.allJava})
    classpath = files(subprojects.collect {it.sourceSets.main.compileClasspath})
    scalaClasspath = subprojects.first().scaladoc.scalaClasspath

    subprojects.each {
        excludes += it.scaladoc.excludes
        includes += it.scaladoc.includes
    }
}

task testReport(type: TestReport) {
    description = "Aggregate all test results from subprojects into one test report."
    
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
    dependsOn(subprojects.check)
}

task uploadTestReports(type: S3Upload, dependsOn: testReport) {
    description = "Upload Gradle test reports to S3."
    dependsOn testReport
    
    String repoSlug = TravisEnvironment.repoSlug()
    String branch = TravisEnvironment.branch()
    String buildNumber = TravisEnvironment.buildNumber()
    
    region = "us-west-2"
    bucket = "usi-builds"
    prefix = "$repoSlug/$branch/$buildNumber"
    source = testReport.destinationDir
}

task githubReport(type: GithubStatus, dependsOn: uploadTestReports) {
    description = "Report test results to Github as commit status."
    dependsOn uploadTestReports
    
    repoSlug = TravisEnvironment.repoSlug()
    commit = TravisEnvironment.commit()
    context = "mesosphere/tests"
    statusDescription = "Test results"
    targetUrl = "https://s3-us-west-2.amazonaws.com/${uploadTestReports.bucket}/${uploadTestReports.prefix}/index.html"
}

gitPublish {
    repoUri = 'git@github.com:mesosphere/usi.git'
    branch = 'gh-pages'

    contents {
        from "docs"
    }

    preserve {
        // Preserve all files from /docs on master.
        include '_config.yml'
        include '**/*.md'
    }
}
gitPublishCopy.dependsOn(scaladoc)

task ci {
    description = "The main traget for CI builds."
    if (TravisEnvironment.isMasterBuild()) {
        dependsOn githubReport, gitPublishPush
    } else if (TravisEnvironment.isSecureBuild())  {
        dependsOn githubReport
    }
}
